plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.github.plnice.canidropjetifier" version "0.5"
    id 'com.mikepenz.aboutlibraries.plugin'
}

repositories {
    jcenter()
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 30
        versionCode 12
        versionName "2.0"
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    signingConfigs {
        release {
            Properties keyProps = new Properties()
            File keyProperties = file('../keystore.roperties')
            if (keyProperties.exists()) {
                // when building locally, use signing properties from keystore.properties
                keyProps.load(new FileInputStream(keyProperties))
            } else {
                // when building CI server, use signing properties from environment variables
                keyProps.setProperty('storePass', System.getenv('RELEASE_KEYSTORE_PASSWORD') ?: "")
                keyProps.setProperty('pass', System.getenv('RELEASE_KEYSTORE_PASSWORD') ?: "")
                keyProps.setProperty('alias', System.getenv('RELEASE_KEY_ALIAS') ?: "")
                keyProps.setProperty('store', '../awakedebug.keystore')
            }

            storeFile file(keyProps['store'])
            keyAlias keyProps['alias']
            storePassword keyProps['storePass']
            keyPassword keyProps['pass']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude "DebugProbesKt.bin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.mikepenz:aboutlibraries:8.5.0"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}
